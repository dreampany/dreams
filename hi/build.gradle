plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId 'com.dreampany.hi'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName '0.0.1'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        debug {
            storeFile file('../keystore/debug.keystore.jks')
            keyAlias 'debug.keystore'
            storePassword 'debug.keystore'
            keyPassword 'debug.keystore'
        }

        release {
            storeFile file('../keystore/dreampany.keystore.jks')
            keyAlias 'dreampany'
            storePassword 'ROOT.net080208ROOT.net080208'
            keyPassword 'ROOT.net080208ROOT.net080208'
        }
    }

    buildTypes {
        debug {
            crunchPngs false
            minifyEnabled false
            shrinkResources false
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
            jniDebuggable = true
            renderscriptDebuggable = true
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    applicationVariants.all { variant ->
        def parts = variant.applicationId.split('\\.')
        variant.outputs.all {
            outputFileName = parts[2] + "-${variant.versionName}-${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    /* libs */
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /* test */
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    /* library */
    implementation project(':common')

    /* room */
    api "androidx.room:room-runtime:$room_version"
    api "androidx.room:room-ktx:$room_version"
    api 'androidx.paging:paging-runtime-ktx:3.0.0'
    kapt "androidx.room:room-compiler:$room_version"

    /* hilt */
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    /* firebase */
    implementation platform('com.google.firebase:firebase-bom:28.0.1')
    //implementation 'com.google.firebase:firebase-ads'
    implementation 'com.google.firebase:firebase-appindexing'
    //implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-database-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-common-ktx'

    /* google */
    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    /* facebook */
    implementation 'com.facebook.fresco:fresco:2.4.0'
    implementation 'com.facebook.android:facebook-login:9.1.1'

    /* misc */
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.intuit.sdp:sdp-android:1.0.6'
    implementation 'com.intuit.ssp:ssp-android:1.0.6'
    implementation 'com.github.pwittchen:prefser-rx2:2.2.3'
}